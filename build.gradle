import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'org.springframework.boot' version '2.6.4'
    id 'org.flywaydb.flyway' version '8.5.4'
    id 'org.owasp.dependencycheck' version '6.5.3'
    id 'org.sonarqube' version '3.3'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url = 'https://app.camunda.com/nexus/content/repositories/camunda-bpm-ee'
        credentials {
            username "$CAMUNDA_NEXUS_USER"
            password "$CAMUNDA_NEXUS_PASSWORD"
        }
    }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'ee'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

def versions = [
        reformLogging: '5.1.9',
        junit: '5.8.2',
        junitPlatform   : '1.8.2',

]
ext['spring-security.version'] = '5.6.2'

checkstyle {
    maxWarnings = 0
    toolVersion = '8.21'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'
    analyzers.assemblyEnabled = false
}

sourceSets {

    testUtils {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/testUtils/java')
        }
        resources.srcDir file('src/testUtils/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }

    functionalTest {
        java {
            compileClasspath += testUtils.output
            runtimeClasspath += testUtils.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    integrationTest {
        java {
            compileClasspath += testUtils.output
            runtimeClasspath += testUtils.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }


}

task smoke(type: Test) {
    description = "Runs Smoke Tests"
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
    description = "Runs integration tests"
    group = "Verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    failFast = true
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.required = true
        csv.required = false
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "Reform :: camunda-bpm"
        property "sonar.projectKey", "uk.gov.hmcts.reform:camunda-bpm"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
        property "sonar.coverage.exclusions", "**/config/**"
    }
}

ext.libraries = [
    "rest-assured": { version = '4.3.1' },
    junit5: [
        "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
        "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
        "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
        "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
        "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
    ]
]

dependencyManagement {
    imports {
        mavenBom 'org.camunda.bpm.extension.springboot:camunda-bpm-spring-boot-starter-bom:2.2.0'
    }
    imports {
        mavenBom 'org.camunda.bpm:camunda-bom:7.15.0-ee'
    }
    dependencies {
        // CVE-2020-26945 - Mishandles deserialization of object streams.
        dependency group: 'org.mybatis', name: 'mybatis', version: '3.5.9'
        // CVE-2020-29242, CVE-2020-29243, CVE-2020-29244, CVE-2020-29245 - check spring-boot-starter-oauth2-client for lang-tag update
        dependency group: 'com.nimbusds', name: 'lang-tag', version: '1.5'
        // CVE-2020-13956 - misinterpret malformed authority component in request URIs - check for update available to com.warrenstrange:googleauth in service-auth-provider-client
        dependency group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
        // CVE-2020-8908 - Temp directory creation vulnerability
        dependencySet(group: 'com.google.guava', version: '31.1-jre') {
            entry 'guava'
        }

        // CVE-2021-35515, CVE-2021-35516, CVE-2021-35517, CVE-2021-36090
        dependency group: 'org.apache.commons', name: 'commons-compress', version: '1.21'
        dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

        //CVE-2021-42550
        dependencySet(group: 'ch.qos.logback', version: '1.2.10') {
            entry 'logback-classic'
            entry 'logback-core'
        }

        dependencySet(group: 'org.seleniumhq.selenium', version: '4.1.2'){
            entry 'selenium-java'
            entry 'selenium-remote-driver'
            entry 'selenium-firefox-driver'
            entry 'selenium-chrome-driver'
            entry 'selenium-safari-driver'
            entry 'selenium-edge-driver'
            entry 'selenium-api'
            entry 'selenium-support'
        }

        dependency group: 'org.springframework.security', name: 'spring-security-core', version: '5.6.2'
    }
}

dependencies {
    implementation 'org.camunda.bpm.springboot:camunda-bpm-spring-boot-starter-webapp-ee:7.15.4-ee'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.5.1.RELEASE'
    implementation 'org.camunda.bpm.springboot:camunda-bpm-spring-boot-starter-rest:7.16.0'
    implementation 'org.camunda.spin:camunda-spin-dataformat-all:1.13.0'
    implementation 'org.camunda.bpm:camunda-engine-plugin-spin'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.postgresql:postgresql:42.3.3'
    implementation 'org.flywaydb:flyway-core:8.5.1'
    // JAX-B dependencies for JDK 9+
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '3.0.2'

    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
    implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.camunda.bpm.extension:camunda-bpm-assert:1.2'
    testImplementation 'org.camunda.bpm.extension:camunda-bpm-assert-scenario:1.1.1'
    testImplementation 'org.slf4j:jul-to-slf4j:1.7.36'
    testImplementation 'org.testcontainers:postgresql:1.16.3'
    testImplementation group: 'com.github.tomakehurst', name:'wiremock-jre8', version: '2.32.0'
    testImplementation "org.junit.vintage:junit-vintage-engine:${versions.junit}"

    testImplementation libraries.junit5
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testUtilsImplementation sourceSets.main.runtimeClasspath
    testUtilsImplementation sourceSets.test.runtimeClasspath

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath
    integrationTestImplementation "org.junit.vintage:junit-vintage-engine:${versions.junit}"

    smokeTestImplementation sourceSets.main.runtimeClasspath
    smokeTestImplementation sourceSets.test.runtimeClasspath
    smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured'

}

group = 'uk.gov.hmcts.reform'
version = '1.0-SNAPSHOT'
description = 'Camunda BPM Spring Boot Application'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

mainClassName = 'uk.gov.hmcts.reform.camunda.bpm.CamundaApplication'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

flyway {
    url = System.getenv('FLYWAY_URL')
    user = System.getenv('FLYWAY_USER')
    password = System.getenv('FLYWAY_PASSWORD')
    baselineOnMigrate = true
    baselineVersion = '000'
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
    baselineOnMigrate = true
    if (project.hasProperty("dburl")) {
        url = "jdbc:postgresql://${dburl}"
    }
}

bootJar {

    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"

    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
                details.useVersion '2.13.1'
            }
            if (details.requested.group in ['org.apache.tomcat.embed']) {
                details.useVersion '9.0.58'
            }
            // Rest Assured 4.3.0 uses groovy 3
            if (details.requested.group in ['org.codehaus.groovy']) {
                details.useVersion '3.0.5'
            }
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
